<?xml version="1.0" encoding="utf-8"?>
<s:List xmlns:fx="http://ns.adobe.com/mxml/2009"
		xmlns:s="library://ns.adobe.com/flex/spark"
		xmlns:mx="library://ns.adobe.com/flex/mx"
		skinClass="be.irail.betrains.playbook.view.components.skins.BetrainListSkin"
		maxWidth="1024"
		requireSelection="false"
		allowMultipleSelection="false"
		click="list_clickHandler(event)"
		creationComplete="list1_creationCompleteHandler(event)">
	<s:layout>
		<s:VerticalLayout useVirtualLayout="false"
						  gap="0"
						  verticalAlign="{getStyle('verticalAlignContent') ? getStyle('verticalAlignContent') : 'middle'}"
						  variableRowHeight="false"/>
	</s:layout>

	<fx:Metadata>
		[Style(name="verticalAlignContent",format="String",enumeration="bottom,middle,top")]
	</fx:Metadata>

	<fx:Script>
		<![CDATA[
			import mx.events.FlexEvent;

			import spark.components.IItemRenderer;
			import spark.events.IndexChangeEvent;

			private var _lastIndex:int = -99;

			private function list_clickHandler(event:MouseEvent):void {
				if (this.selectedIndex == _lastIndex && (event.target is IItemRenderer && IItemRenderer(event.target).selected)) {
					this.selectedIndex = -1;

					var e:IndexChangeEvent = new IndexChangeEvent(IndexChangeEvent.CHANGE);
					e.oldIndex = _lastIndex;
					e.newIndex = selectedIndex;

					dispatchEvent(e);
				}
				_lastIndex = this.selectedIndex;
			}

			protected function list1_creationCompleteHandler(event:FlexEvent):void {
				if (Multitouch.supportsTouchEvents) {
					Multitouch.inputMode = MultitouchInputMode.TOUCH_POINT;

					stage.addEventListener(TouchEvent.TOUCH_BEGIN, onTouchBegin);
					stage.addEventListener(TouchEvent.TOUCH_MOVE, onTouchMove);
					stage.addEventListener(TouchEvent.TOUCH_END, onTouchEnd);
				}
			}

			private var prevY:Number;

			private function onTouchBegin(event:TouchEvent):void {
				prevY = event.stageY;
			}

			private function onTouchMove(event:TouchEvent):void {
				var deltaY:Number = Math.round((prevY - event.stageY));
				var scrollUp:Boolean = (deltaY < 0);
				var desiredScrollPosition:Number = dataGroup.verticalScrollPosition + deltaY;

				if (desiredScrollPosition < 0) {
					desiredScrollPosition = 0;
				} else if (desiredScrollPosition > dataGroup.contentHeight) {
					desiredScrollPosition = dataGroup.contentHeight;
				}

				dataGroup.verticalScrollPosition = desiredScrollPosition;
				prevY = event.stageY;
			}

			private function onTouchEnd(event:TouchEvent):void {

			}
		]]>
	</fx:Script>

	<fx:Declarations>
		<fx:Boolean id="borderVisible">true</fx:Boolean>
		<fx:String id="hScrollPolicy">off</fx:String>
		<fx:String id="vScrollPolicy">auto</fx:String>
	</fx:Declarations>
</s:List>
