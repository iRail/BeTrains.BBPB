<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009"
		 xmlns:s="library://ns.adobe.com/flex/spark"
		 xmlns:mx="library://ns.adobe.com/flex/mx"
		 resize="group1_resizeHandler(event)"
		 creationComplete="createList();"
		 xmlns:ns="http://ns.renaun.com/mxml/2010"
		 xmlns:listClasses="qnx.ui.listClasses.*">
	<fx:Metadata>
		[Event(name="change",type="flash.events.Event")]
	</fx:Metadata>
	<fx:Script>
		<![CDATA[
			import be.irail.api.data.stations.IRStation;

			import mx.collections.ArrayCollection;
			import mx.events.ResizeEvent;

			import qnx.ui.data.SectionDataProvider;
			import qnx.ui.events.ListEvent;
			import qnx.ui.listClasses.ListSelectionMode;
			import qnx.ui.skins.listClasses.SectionListHeaderSkinWhite;

			// ----------------------------
			// dataProvider
			// ----------------------------

			private var _dataProvider:ArrayCollection;

			[Bindable(event="dataProviderChange")]
			public function get dataProvider():ArrayCollection {
				return _dataProvider;
			}

			public function set dataProvider(value:ArrayCollection):void {
				_dataProvider = value;
				updateDataProvider();
				dispatchEvent(new Event("dataProviderChange"));
			}

			// ----------------------------
			// selectedItem
			// ----------------------------

			[Bindable(event="selectedItemChange")]
			public function get selectedItem():Object {
				if (!list || !list.selectedItem)
					return null;

				return list.selectedItem.value;
			}

			public function set selectedItem(value:Object):void {
				if (list) {

					if (!value) {
						list.selectedItems = [];
						dispatchEvent(new Event(Event.CHANGE));
						dispatchEvent(new Event("selectedItemChange"));
						return;
					}

					var numDPItems:int = list.dataProvider.length, i:int;
					for (i = 0; i < numDPItems; i++) {
						var val:Object = list.dataProvider.getItemAt(i);
						if (val.value == value) {
							list.selectedItem = val;
							break;
						}
					}
					dispatchEvent(new Event(Event.CHANGE));
					dispatchEvent(new Event("selectedItemChange"));
				}
			}

			// ----------------------------
			// selectedIndex
			// ----------------------------


			[Bindable(event="selectedIndexChange")]
			public function get selectedIndex():int {
				if (!list || !list.selectedItem)
					return -1;

				return list.selectedIndex;
			}

			public function set selectedIndex(value:int):void {
				if (value > -1) {
					this.selectedItem = list.dataProvider.getItemAt(value);
				} else {
					this.selectedItem = null;
				}
				dispatchEvent(new Event("selectedIndexChange"));
			}

			private function createList():void {
				list.setSize(this.width, this.height);
				list.addEventListener(ListEvent.ITEM_CLICKED, onListItemClicked);
				updateDataProvider();
			}

			private function onListItemClicked(event:ListEvent):void {
				dispatchEvent(new Event(Event.CHANGE));
			}

			private function group1_resizeHandler(event:ResizeEvent):void {
				list.setSize(this.width, this.height);
			}

			private function updateDataProvider():void {
				if (!list || !dataProvider || !dataProvider.length) {
					return
				}

				var alphabet:Array = [],
					dpNum:int = dataProvider.length,
					station:IRStation,
					char:String;

				while (--dpNum > -1) {
					station = dataProvider.getItemAt(dpNum) as IRStation;
					char = station.name.substr(0, 1).toUpperCase();

					if (alphabet.indexOf(char) == -1) {
						alphabet.push(char);
					}
					station = null;
					char = null;
				}

				dpNum = dataProvider.length;
				alphabet.sort();

				var sectionDP:SectionDataProvider = new SectionDataProvider();

				var sectionId:int = 0;
				for (sectionId = 0; sectionId < alphabet.length; sectionId++) {
					sectionDP.addItem({label: alphabet[sectionId]});
				}

				var stationId:int = 0;
				for (stationId = 0; stationId < dpNum; stationId++) {
					station = dataProvider.getItemAt(stationId) as IRStation;
					char = station.name.substr(0, 1).toUpperCase();

					var sectionIndex:int = alphabet.indexOf(char);
					sectionDP.addChildToIndex({label: station.formattedName, value: station}, sectionIndex);

					station = null;
					char = null;
				}

				list.dataProvider = sectionDP;
			}
		]]>
	</fx:Script>
	<fx:Declarations>
	</fx:Declarations>
	<ns:QContainer>
		<listClasses:SectionList id="list"
								 selectionMode="{ListSelectionMode.SINGLE}"
								 headerHeight="40"/>
	</ns:QContainer>
</s:Group>
