<?xml version="1.0" encoding="utf-8"?>
<s:SkinnableContainer xmlns:fx="http://ns.adobe.com/mxml/2009"
					  xmlns:s="library://ns.adobe.com/flex/spark"
					  xmlns:mx="library://ns.adobe.com/flex/mx"
					  width="100%"
					  height="74"
					  skinClass="be.irail.betrains.playbook.view.components.skins.ApplicationBarSkin"
					  xmlns:components="be.irail.betrains.playbook.view.components.*"
					  creationComplete="creationCompleteHandler()"
					  xmlns:controls="be.irail.betrains.playbook.view.components.controls.*">
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<fx:Metadata>
		[Event(name="change",type="spark.events.IndexChangeEvent")]
	</fx:Metadata>

	<fx:Script>
		<![CDATA[
			include "../resource_wrapper.as";

			import be.irail.betrains.playbook.view.components.controls.ApplicationBarButton;
			import be.irail.betrains.playbook.controller.ModelLocator;

			import com.greensock.TweenLite;
			import com.greensock.easing.Expo;

			import mx.core.IVisualElement;

			import qnx.locale.LocaleManager;

			import spark.events.IndexChangeEvent;

			public const ABOUT_INDEX:uint = 100;

			[Bindable]
			private var _model:ModelLocator = ModelLocator.getInstance();

			private var _created:Boolean;

			// ----------------------------
			// selectedIndex
			// ----------------------------

			private var _selectedIndex:int = -1;

			[Bindable(event="change")]
			public function get selectedIndex():int {
				return _selectedIndex;
			}

			public function set selectedIndex(value:int):void {
				if (value != _selectedIndex) {
					var oldIndex:int = _selectedIndex;
					_selectedIndex = value;
					applySelectedIndex();
					dispatchEvent(new IndexChangeEvent(IndexChangeEvent.CHANGE, false, false, oldIndex, value));
				}
			}

			private function applySelectedIndex():void {
				if (_created) {
					unselectAll();
					var isFirst:Boolean;
					if (_selectedIndex == -1) {
						_selectedIndex = 0;
						isFirst = true;
						dispatchEvent(new IndexChangeEvent(IndexChangeEvent.CHANGE, false, false, -1, 0));
					}

					if (_selectedIndex != ABOUT_INDEX) {
						var btn:ApplicationBarButton = getButtonByIndex(_selectedIndex),
							newX:int = btn.x,
							newW:int = btn.width;

						if (!isFirst) {
							TweenLite.to(indicator, .6, {x: newX, ease: Expo.easeOut, onComplete: function():void {
								btn.selected = true;
							}
										 });
						} else {
							indicator.x = newX;
							indicator.width = newW;
							indicator.visible = true;
						}
					}
				}
			}

			private function getButtonByIndex(index:int):ApplicationBarButton {
				var i:int = 0, count:int = 0, element:IVisualElement;
				for (i = 0; i < buttons.numElements; i++) {
					element = buttons.getElementAt(i);
					if (element is ApplicationBarButton) {
						if (count == index) {
							return ApplicationBarButton(element);
						}
						count++;
					}
				}
				return null;
			}

			private function getButtonIndex(btn:ApplicationBarButton):int {
				if (btn == about) {
					return ABOUT_INDEX;
				}

				var i:int = 0, count:int = 0, element:IVisualElement;
				for (i = 0; i < buttons.numElements; i++) {
					element = buttons.getElementAt(i);
					if (element is ApplicationBarButton) {
						if (element === btn) {
							return count;
						}
						count++;
					}
				}
				return -1;
			}

			private function getSelectedButtonIndex():int {
				var i:int = 0, count:int = 0, element:IVisualElement;
				for (i = 0; i < buttons.numElements; i++) {
					element = buttons.getElementAt(i);
					if (element is ApplicationBarButton) {
						if (ApplicationBarButton(element).selected) {
							return count;
						}
						count++;
					}
				}
				return 0;
			}

			private function unselectAll():void {
				var i:int = 0, count:int = 0, element:IVisualElement;
				for (i = 0; i < buttons.numElements; i++) {
					element = buttons.getElementAt(i);
					if (element is ApplicationBarButton) {
						ApplicationBarButton(element).selected = false;
					}
				}
			}

			private function creationCompleteHandler():void {
				_created = true;
				applySelectedIndex();
			}

			protected function changeHandler(event:Event):void {
				var btn:ApplicationBarButton = ApplicationBarButton(event.target);
				var index:int = getButtonIndex(btn);
				if (index == selectedIndex) {
					btn.selected = true;
				} else {
					selectedIndex = index;
				}
			}
		]]>
	</fx:Script>

	<s:Group id="buttons"
			 horizontalCenter="0"
			 minWidth="700"
			 maxWidth="1024"
			 top="1">
		<s:layout>
			<s:TileLayout columnAlign="justifyUsingWidth"
						  rowAlign="justifyUsingHeight"
						  orientation="columns"
						  verticalAlign="middle"
						  requestedRowCount="1"/>
		</s:layout>

		<s:Rect id="indicator"
				height="{buttons.height}"
				topLeftRadiusX="4"
				topLeftRadiusY="4"
				topRightRadiusX="4"
				topRightRadiusY="4"
				includeInLayout="false"
				visible="false">
			<s:fill>
				<s:SolidColor color="white"
							  alpha=".6"/>
			</s:fill>
			<s:filters>
				<s:DropShadowFilter inner="true"
									blurX="2"
									blurY="2"
									color="0"
									distance="1"
									angle="90"/>
			</s:filters>
		</s:Rect>

		<controls:ApplicationBarButton id="recents"
									   label="{r('application.controlbar.recent')}"
									   image="@Embed(source='images/time.png')"
									   change="changeHandler(event)"/>
		<controls:ApplicationBarButton id="favs"
									   label="{r('application.controlbar.favs')}"
									   image="@Embed(source='images/favs.png')"
									   change="changeHandler(event)"/>
		<controls:ApplicationBarButton id="schedule"
									   label="{r('application.controlbar.scheduler')}"
									   image="@Embed(source='images/train.png')"
									   change="changeHandler(event)"/>
		<controls:ApplicationBarButton id="stations"
									   label="{r('application.controlbar.stations')}"
									   image="@Embed(source='images/stations.png')"
									   change="changeHandler(event)"/>
		<controls:ApplicationBarButton id="works"
									   label="{r('application.controlbar.works')}"
									   image="@Embed(source='images/warning.png')"
									   change="changeHandler(event)"/>
		<!--<components:ApplicationBarButton id="chat"
										 label="{r('application.controlbar.chat')}"
										 image="@Embed(source='images/chat.png')"
										 change="changeHandler(event)"
										 enabled="{_model.loggedIn}"/>-->
	</s:Group>
	<controls:ApplicationBarButton id="about"
								   label="{r('application.controlbar.about')}"
								   image="@Embed(source='images/about.png')"
								   right="40"
								   change="changeHandler(event)"/>
</s:SkinnableContainer>
