<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009"
		 xmlns:s="library://ns.adobe.com/flex/spark"
		 xmlns:mx="library://ns.adobe.com/flex/mx"
		 xmlns:stations="be.irail.api.data.stations.*"
		 xmlns:components="be.irail.betrains.playbook.view.components.*"
		 xmlns:stations1="be.irail.betrains.playbook.view.stations.*"
		 xmlns:ns="http://ns.renaun.com/mxml/2010"
		 xmlns:yahoo="http://www.yahoo.com/2008/mxml"
		 creationComplete="group1_creationCompleteHandler(event)">
	<s:states>
		<s:State name="none"/>
		<s:State name="details"/>
	</s:states>
	<fx:Script>
		<![CDATA[
			import be.irail.api.data.liveboard.Liveboard;
			import be.irail.api.data.stations.IRStation;
			import be.irail.api.event.IRailErrorEvent;
			import be.irail.api.event.IRailResultEvent;
			import be.irail.api.methodgroup.Liveboards;
			import be.irail.betrains.playbook.controller.AppSettings;
			import be.irail.betrains.playbook.utils.PopUpUtil;

			import com.yahoo.maps.api.YahooMap;
			import com.yahoo.maps.api.YahooMapEvent;
			import com.yahoo.maps.api.core.location.LatLon;

			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.events.ResizeEvent;

			include "../resource_wrapper.as"

			private var _mapReady:Boolean;

			private var service:Liveboards = new Liveboards();

			private var mapComponent:YahooMap;

			private var stationsLiveboard:StationsLiveboardInfo;

			private var _refreshTimer:Timer;

			// ----------------------------
			// station
			// ----------------------------

			private var _station:IRStation;

			[Bindable(event="stationChange")]
			public function get station():IRStation {
				return _station;
			}

			[Bindable(event="stationChange")]
			public function get stationName():String {
				if (!station) {
					return "";
				}
				return station.formattedName;
			}

			[Bindable(event="stationChange")]
			public function get stationHasLocation():Boolean {
				if (!station) {
					return false;
				}
				return station.hasLocation;
			}

			[Bindable(event="stationChange")]
			public function get stationLng():Number {
				if (!station) {
					return 0;
				}
				return station.longitude;
			}

			[Bindable(event="stationChange")]
			public function get stationLat():Number {
				if (!station) {
					return 0;
				}
				return station.latitude;
			}

			public function set station(value:IRStation):void {
				_station = value;

				stopTimer();

				if (value) {
					this.currentState = "details";
				} else {
					this.currentState = "none";
				}

				addMarker();

				dispatchEvent(new Event("stationChange"));
			}

			private function addMarker():void {
				if (mapComponent && _mapReady && stationHasLocation) {
					mapComponent.centerLatLon = new LatLon(stationLat, stationLng);
					mapComponent.zoomLevel = 3;
				}
			}

			protected function liveBoard_clickHandler(event:MouseEvent):void {
				service.addEventListener(IRailErrorEvent.API_ERROR, onError);
				service.addEventListener(IRailErrorEvent.IO_ERROR, onError);

				loadLiveBoard();
			}

			private function loadLiveBoard():void {
				service.getStationLiveboardByStationName(stationName);
				service.addEventListener(IRailResultEvent.LIVEBOARD_RESULT, onLiveboardResult);
				liveBoardBtn.isBusy = true;
			}

			private function onError(event:IRailErrorEvent):void {
				liveBoardBtn.isBusy = false;
				startRefreshTimer();
			}

			private function onLiveboardResult(event:IRailResultEvent):void {
				service.removeEventListener(IRailResultEvent.LIVEBOARD_RESULT, onLiveboardResult);
				liveBoardBtn.isBusy = false;

				if (!(event.result && event.result.data && event.result.data.station)) {
					return;
				}

				if (stationsLiveboard && stationsLiveboard.parent) {
					stationsLiveboard.liveBoard = null;
				} else {
					stationsLiveboard = new StationsLiveboardInfo();
					stationsLiveboard.addEventListener(CloseEvent.CLOSE, onLiveboardPopupClose);
					PopUpUtil.openPopUp(stationsLiveboard, true);
				}

				stationsLiveboard.liveBoard = (event.result.data as Liveboard);

				startRefreshTimer();
			}

			private function onLiveboardPopupClose(event:CloseEvent):void {
				service.removeEventListener(IRailResultEvent.LIVEBOARD_RESULT, onLiveboardResult);
				service.removeEventListener(IRailErrorEvent.API_ERROR, onError);
				service.removeEventListener(IRailErrorEvent.IO_ERROR, onError);

				stopTimer();

				stationsLiveboard.removeEventListener(CloseEvent.CLOSE, onLiveboardPopupClose);
				stationsLiveboard = null;

			}

			private function startRefreshTimer():void {
				stopTimer();

				if (stationsLiveboard && stationsLiveboard.parent) {

					_refreshTimer = new Timer(AppSettings.LIVEBOARD_REFRESH_INTERVAL * 1000, 1);
					_refreshTimer.addEventListener(TimerEvent.TIMER_COMPLETE, onRefreshLiveboard);
					_refreshTimer.start();

				}
			}

			private function stopTimer():void {
				if (_refreshTimer) {
					_refreshTimer.removeEventListener(TimerEvent.TIMER_COMPLETE, onRefreshLiveboard);
					_refreshTimer.stop();
				}
				_refreshTimer = null;
			}

			private function onRefreshLiveboard(e:TimerEvent):void {
				_refreshTimer.removeEventListener(TimerEvent.TIMER_COMPLETE, onRefreshLiveboard);

				if (stationsLiveboard && stationsLiveboard.parent) {
					loadLiveBoard();
				} else {
					stationsLiveboard.liveBoard = null;
					stationsLiveboard = null;
				}
			}

			protected function group1_creationCompleteHandler(event:FlexEvent):void {
				mapComponent = new YahooMap();
				mapComponent.init("Hc6WPw_V34Fs6FRv3XHq8vDAcXKBhZuS0gzHba0Sq8PKwD3n4NZQtS1pv7vuwsPa3I6unns4", mapComponentContainer.width, mapComponentContainer.height);

				mapComponent.addEventListener(YahooMapEvent.MAP_INITIALIZE, onMapInitialize);
				mapComponentContainer.addChild(mapComponent);
			}

			private function onMapInitialize(event:YahooMapEvent):void {
				mapComponent.removeEventListener(YahooMapEvent.MAP_INITIALIZE, onMapInitialize);
				mapComponent.addPanControl();
				mapComponent.addScaleBar();
				mapComponent.addZoomWidget();

				mapComponent.zoomWidget.scaleX = mapComponent.zoomWidget.scaleY = 2.2;

				_mapReady = true;

				addMarker();
			}

			protected function mapComponentContainer_resizeHandler(event:ResizeEvent):void {
				if (mapComponent) {
					mapComponent.width = mapComponentContainer.width;
					mapComponent.height = mapComponentContainer.height;
				}
			}
		]]>
	</fx:Script>

	<s:Label styleName="noneLabel"
			 alpha=".3"
			 left="10"
			 top="10"
			 horizontalCenter="0"
			 verticalCenter="0"
			 includeIn="none"
			 text="{r('station.profile.none')}"/>

	<s:Group includeIn="details"
			 top="10"
			 width="100%"
			 height="100%">
		<s:Label textAlign="left"
				 paddingLeft="10"
				 width="{this.width}"
				 fontSize="60"
				 color="#1F1F1F"
				 alpha=".6"
				 fontWeight="bold"
				 maxDisplayedLines="1"
				 text="{stationName}"/>
		<s:Label textAlign="left"
				 paddingLeft="10"
				 width="{this.width}"
				 y="45"
				 fontSize="30"
				 color="#1F1F1F"
				 alpha=".4"
				 fontWeight="bold"
				 text="{r('station.profile.type.station')}"/>

		<stations1:StationDataButton id="liveBoardBtn"
									 y="150"
									 left="0"
									 right="10"
									 horizontalCenter="0"
									 label="{r('station.profile.liveboardbtn')}"
									 click="liveBoard_clickHandler(event)"/>
	</s:Group>

	<mx:UIComponent id="mapComponentContainer"
					top="280"
					left="0"
					right="10"
					height="245"
					horizontalCenter="0"
					visible="{stationHasLocation}"
					resize="mapComponentContainer_resizeHandler(event)"/>
</s:Group>
