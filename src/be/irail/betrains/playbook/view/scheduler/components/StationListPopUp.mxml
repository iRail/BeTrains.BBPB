<?xml version="1.0" encoding="utf-8"?>
<s:TitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009"
			   xmlns:s="library://ns.adobe.com/flex/spark"
			   xmlns:mx="library://ns.adobe.com/flex/mx"
			   width="1024"
			   height="550"
			   skinClass="be.irail.betrains.playbook.view.components.skins.StationPopupSkin"
			   xmlns:stations="be.irail.api.data.stations.*"
			   creationComplete="titlewindow1_creationCompleteHandler(event)"
			   contentCreationComplete="titlewindow1_contentCreationCompleteHandler(event)"
			   xmlns:components="be.irail.betrains.playbook.view.components.*"
			   xmlns:ns="http://ns.renaun.com/mxml/2010"
			   xmlns:text="qnx.ui.text.*">
	<fx:Declarations>
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import be.irail.api.data.stations.IRStation;
			import be.irail.betrains.playbook.controller.ModelLocator;
			import be.irail.betrains.playbook.view.scheduler.components.itemRenderer.StationListItem;

			import mx.collections.ArrayCollection;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;

			import org.as3commons.lang.StringUtils;

			import qnx.locale.LocaleManager;
			import qnx.ui.skins.text.TextInputSkinWhite;
			import qnx.ui.text.TextInput;
			import qnx.ui.text.TextInputIconMode;

			import spark.events.IndexChangeEvent;

			public static const OK:uint = 0x0004;

			public static const CANCEL:uint = 0x0008;

			private var _model:ModelLocator = ModelLocator.getInstance();

			// ----------------------------
			// stations (read-only)
			// ----------------------------

			private var _stationList:ArrayCollection;

			private var _nameFilter:String;

			[Bindable(event="stationListChange")]
			public function get stations():ArrayCollection {
				return _stationList;
			}

			// ----------------------------
			// selectedStation
			// ----------------------------

			private var _selectedStation:IRStation;

			public function get selectedStation():IRStation {
				return _selectedStation;
			}

			public function set selectedStation(value:IRStation):void {
				if (value != _selectedStation) {
					_selectedStation = value;
				}
			}

			public function close(detail:uint = 0x0008):void {
				stage.focus = null;
				PopUpManager.removePopUp(this);
				dispatchEvent(new CloseEvent(CloseEvent.CLOSE, false, false, detail));
			}

			protected function stationName_changeHandler(e:Event):void {
				var txtInput:TextInput = e.currentTarget as TextInput;
				_nameFilter = StringUtils.trim(txtInput.text);
				refreshList();
			}


			protected function titlewindow1_creationCompleteHandler(event:FlexEvent):void {
				_stationList = new ArrayCollection(_model.stations.toArray());
				_stationList.filterFunction = filterList;
				refreshList();
			}

			private function filterList(obj:Object):Boolean {
				if (!_nameFilter || _nameFilter.length == 0) {
					return true;
				}

				var name:String = String(obj.name).toLowerCase().substr(0, _nameFilter.length),
					test:RegExp = new RegExp(_nameFilter, "ig"),
					result:Array = name.match(test);

				return (result && result.length > 0);
			}

			private function refreshList():void {
				_stationList.refresh();
				dispatchEvent(new Event("stationListChange"));
			}

			private function stationList_changeHandler(event:IndexChangeEvent):void {
				selectedStation = (stationList.selectedItem as IRStation);
				close(OK);
			}

			protected function titlewindow1_contentCreationCompleteHandler(event:FlexEvent):void {
				txtInput.setSkin(TextInputSkinWhite);
			}


			private function closeButtonClose():void {
				selectedStation = null;
				close(CANCEL);
			}
		]]>
	</fx:Script>
	<s:Group width="800"
			 horizontalCenter="0">
		<ns:QContainer width="800"
					   height="50"
					   y="15"
					   x="-3"
					   padding="0">
			<text:TextInput id="txtInput"
							width="800"
							height="50"
							prompt="{LocaleManager.localeManager.getResource('station.search.prompt')}"
							change="stationName_changeHandler(event)"
							clearIconMode="{TextInputIconMode.ALWAYS}"/>
		</ns:QContainer>

		<components:BeTrainsList itemRenderer="be.irail.betrains.playbook.view.scheduler.components.itemRenderer.StationListItem"
								 id="stationList"
								 y="70"
								 width="800"
								 height="470"
								 horizontalCenter="0"
								 labelFunction="IRStation.labelFunction"
								 dataProvider="{stations}"
								 change="stationList_changeHandler(event)"/>

		<components:CloseButton right="-45"
								top="24"
								click="closeButtonClose()"/>

	</s:Group>
</s:TitleWindow>
