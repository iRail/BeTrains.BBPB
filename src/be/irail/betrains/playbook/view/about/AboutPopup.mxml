<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009"
		 xmlns:s="library://ns.adobe.com/flex/spark"
		 xmlns:mx="library:/	/ns.adobe.com/flex/mx"
		 width="800"
		 height="500"
		 creationComplete="init()"
		 xmlns:controls="be.irail.betrains.playbook.view.components.controls.*"
		 xmlns:components="be.irail.betrains.playbook.view.components.*">
	<fx:Declarations>
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import be.irail.betrains.playbook.controller.AppSettings;

			import mx.managers.PopUpManager;

			import qnx.media.QNXStageWebView;

			include "../resource_wrapper.as";

			private var webView:QNXStageWebView;

			// ----------------------------
			// isLoading
			// ----------------------------

			private var _isLoading:Boolean;

			[Bindable(event="isLoadingChange")]
			public function get isLoading():Boolean {
				return _isLoading;
			}

			public function set isLoading(value:Boolean):void {
				if (value != _isLoading) {
					_isLoading = value;
					if (value) {
						spinner.play()
					} else {
						spinner.stop();
					}
					dispatchEvent(new Event("isLoadingChange"));
				}
			}

			// ----------------------------
			// isError
			// ----------------------------

			private var _isError:Boolean;

			[Bindable(event="isErrorChange")]
			public function get isError():Boolean {
				return _isError;
			}

			public function set isError(value:Boolean):void {
				if (value !== _isError) {
					_isError = value;
					dispatchEvent(new Event("isErrorChange"));
				}
			}

			private function init():void {
				if (!webView) {
					webView = new QNXStageWebView();

					webView.addEventListener(Event.COMPLETE, loadWebView);
					webView.addEventListener(ErrorEvent.ERROR, onError);
					webView.addEventListener(IOErrorEvent.IO_ERROR, onIOError);
				}
				webView.visible = false;
				webView.stage = this.stage;

				webView.loadURL(AppSettings.ABOUT_PAGE_URL);
				isLoading = true;
			}

			private function onError(event:ErrorEvent):void {
				trace("WebView Error: ", event.text);
				if (webView) {
					webView.stop();
					webView.executeJavaScript("self.close();");
					webView.visible = false;
				}
				isLoading = false;
				isError = true;
			}

			private function onIOError(event:IOErrorEvent):void {
				trace("WebView IOError: ", event.text);
				if (webView) {
					webView.stop();
					webView.executeJavaScript("self.close();");
					webView.visible = false;
				}
				isLoading = false;
				isError = true;
			}

			private function close():void {
				if (webView) {
					webView.stop();
					webView.executeJavaScript("self.close();");
					webView = null;
				}
				PopUpManager.removePopUp(this);
			}

			private function loadWebView(event:Event):void {
				if (webView) {
					webView.visible = true;
					webView.viewPort = new Rectangle(x + 10, y + 10, this.width - 20, this.height - 20);
				}
				isLoading = false;
			}
		]]>
	</fx:Script>

	<s:Rect id="panel"
			left="0"
			right="0"
			bottom="0"
			top="0"
			radiusX="4"
			radiusY="4">
		<s:fill>
			<s:BitmapFill source="@Embed(source='images/bg.png')"
						  fillMode="scale"
						  smooth="true"/>
		</s:fill>
		<s:filters>
			<s:DropShadowFilter blurX="6"
								blurY="6"
								strength=".4"
								quality="3"
								angle="45"
								distance="3"/>
		</s:filters>
	</s:Rect>

	<controls:Spinner id="spinner"
					  autoPlay="true"
					  visible="{isLoading}"
					  horizontalCenter="0"
					  verticalCenter="0"/>

	<s:Label styleName="noneLabel"
			 alpha=".3"
			 width="100%"
			 height="100%"
			 visible="{isError}"
			 horizontalCenter="0"
			 verticalCenter="0"
			 text="{r('about.error')}"/>

	<controls:CloseButton id="closeBtnGrp"
						  top="-15"
						  right="-20"
						  close="close();"/>

</s:Group>
