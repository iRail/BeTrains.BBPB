<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009"
			   xmlns:s="library://ns.adobe.com/flex/spark"
			   xmlns:mx="library://ns.adobe.com/flex/mx"
			   xmlns:view="be.irail.betrains.playbook.view.*"
			   skinClass="be.irail.betrains.playbook.view.components.skins.ApplicationSkin"
			   frameRate="30"
			   width="1024"
			   height="600"
			   usePreloader="true"
			   preloader="be.irail.betrains.playbook.view.components.preloader.BeTrainsLoader"
			   preinitialize="preinitializeHandler()"
			   applicationComplete="applicationCompleteHandler()"
			   creationComplete="creationCompleteHandler()">
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>

	<fx:Style source="styles.css"/>

	<fx:Script>
		<![CDATA[
			import be.irail.api.core.IRailAPISettings;
			import be.irail.betrains.playbook.collection.FavouriteConnectionCollection;
			import be.irail.betrains.playbook.collection.RecentQueriesCollection;
			import be.irail.betrains.playbook.controller.AppSettings;
			import be.irail.betrains.playbook.controller.ModelLocator;
			import be.irail.betrains.playbook.utils.LocalisationUtil;
			import be.irail.betrains.playbook.utils.SerializeUtil;
			import be.irail.betrains.playbook.view.MainView;

			import mx.collections.ArrayCollection;

			import qnx.locale.LocaleManager;

			private var _model:ModelLocator = ModelLocator.getInstance();

			private function preinitializeHandler():void {
				setupResourceManager();
				readStations();
				readRecents();
				readFavs();
			}

			private function creationCompleteHandler():void {
				NativeApplication.nativeApplication.addEventListener(Event.EXITING, onApplicationExiting);
				callLater(setupMainView);

			/* var deviceFonts:Array = Font.enumerateFonts(true);
			trace("device fonts: ", deviceFonts); */

			/* Alert.show(
				"Supports touch events: " + Multitouch.supportsTouchEvents + "\n" +
				"Maximum supported touch points: " + Multitouch.maxTouchPoints + "\n\n" +
				"Supports gesture events: " + Multitouch.supportsGestureEvents + "\n" +
				"Supported gestures: " + Multitouch.supportedGestures, "The BlackBerry Playbook: Multitouch support"); */
			}

			private function readStations():void {
				_model.stations = new ArrayCollection();

				var stream:FileStream = new FileStream();
				var dataFile:String = "stations_" + IRailAPISettings.mainLanguage + ".bpd";

				if (AppSettings.STATIONSDATA_STORAGE_LOCATION.resolvePath(dataFile).exists) {
					stream.openAsync(AppSettings.STATIONSDATA_STORAGE_LOCATION.resolvePath(dataFile), FileMode.READ);
					stream.addEventListener(Event.COMPLETE, stationDataReadHandler);
					stream.addEventListener(IOErrorEvent.IO_ERROR, readStationsIOErrorHandler);
				}
			}

			private function readRecents():void {
				_model.recentSchedulerQueries = new RecentQueriesCollection();

				if (AppSettings.RECENT_STORAGE_LOCATION.exists) {
					var stream:FileStream = new FileStream();
					stream.openAsync(AppSettings.RECENT_STORAGE_LOCATION, FileMode.READ);
					stream.addEventListener(Event.COMPLETE, recentDataReadHandler);
					stream.addEventListener(IOErrorEvent.IO_ERROR, readRecentIOErrorHandler);
				}

			}

			private function readFavs():void {
				_model.favourites = new FavouriteConnectionCollection();

				if (AppSettings.FAVOURITES_STORAGE_LOCATION.exists) {
					var stream:FileStream = new FileStream();
					stream.openAsync(AppSettings.FAVOURITES_STORAGE_LOCATION, FileMode.READ);
					stream.addEventListener(Event.COMPLETE, favsDataReadHandler);
					stream.addEventListener(IOErrorEvent.IO_ERROR, favsIOErrorHandler);
				}

			}

			private function writeRecents():void {
				if (AppSettings.RECENT_STORAGE_LOCATION.exists) {
					AppSettings.RECENT_STORAGE_LOCATION.deleteFile();
				}

				if (_model.recentSchedulerQueries.length > 0) {
					var stream:FileStream = new FileStream();
					var ba:ByteArray = SerializeUtil.serialize(_model.recentSchedulerQueries.toArray());
					stream.open(AppSettings.RECENT_STORAGE_LOCATION, FileMode.WRITE);
					stream.writeBytes(ba);
				}
			}

			private function writeFavourites():void {
				if (AppSettings.FAVOURITES_STORAGE_LOCATION.exists) {
					AppSettings.FAVOURITES_STORAGE_LOCATION.deleteFile();
				}

				if (_model.favourites.length > 0) {
					var stream:FileStream = new FileStream();
					var ba:ByteArray = SerializeUtil.serialize(_model.favourites.toArray());
					stream.open(AppSettings.FAVOURITES_STORAGE_LOCATION, FileMode.WRITE);
					stream.writeBytes(ba);
				}
			}

			private function stationDataReadHandler(event:Event):void {
				var stream:FileStream = event.target as FileStream;
				var ba:ByteArray = new ByteArray();
				stream.readBytes(ba, 0, stream.bytesAvailable);
				stream.close();

				var data:Object = SerializeUtil.deserialize(ba);
				_model.stations = new ArrayCollection((data.data) as Array);
			}

			private function recentDataReadHandler(event:Event):void {
				var stream:FileStream = event.target as FileStream;
				if (stream.bytesAvailable > 0) {
					var ba:ByteArray = new ByteArray();

					stream.readBytes(ba, 0, stream.bytesAvailable);
					stream.close();

					var data:Object = SerializeUtil.deserialize(ba);
					_model.recentSchedulerQueries = new RecentQueriesCollection(data as Array);
				} else {
					AppSettings.RECENT_STORAGE_LOCATION.deleteFile();
				}
			}

			private function favsDataReadHandler(event:Event):void {
				var stream:FileStream = event.target as FileStream;
				if (stream.bytesAvailable > 0) {
					var ba:ByteArray = new ByteArray();

					stream.readBytes(ba, 0, stream.bytesAvailable);
					stream.close();

					var data:Object = SerializeUtil.deserialize(ba);
					_model.favourites = new FavouriteConnectionCollection(data as Array);
				} else {
					AppSettings.FAVOURITES_STORAGE_LOCATION.deleteFile();
				}
			}

			private function readStationsIOErrorHandler(event:IOErrorEvent):void {
				var stream:FileStream = event.target as FileStream;
				stream.close();
			}

			private function readRecentIOErrorHandler(event:IOErrorEvent):void {
				var stream:FileStream = event.target as FileStream;
				stream.close();
			}

			private function favsIOErrorHandler(event:IOErrorEvent):void {
				var stream:FileStream = event.target as FileStream;
				stream.close();
			}

			private function onApplicationExiting(event:Event):void {
				writeRecents();
				writeFavourites();
			}

			private function setupResourceManager():void {
				var deviceLocale:String = LocaleManager.localeManager.getDeviceLocale();

				IRailAPISettings.mainLanguage = LocalisationUtil.getIRailLangByDeviceLocale(deviceLocale);
				LocaleManager.localeManager.setLocale(LocalisationUtil.getLocalisationByDeviceLocale(deviceLocale));

				LocaleManager.localeManager.dispatchEvent(new Event(Event.CHANGE));
			}

			protected function applicationCompleteHandler():void {
				loaderInfo.uncaughtErrorEvents.addEventListener(UncaughtErrorEvent.UNCAUGHT_ERROR, onUncaughtError);
			}

			private function onUncaughtError(event:UncaughtErrorEvent):void {
			}

			private function setupMainView():void {
				var mv:MainView = new MainView();
				this.addElement(mv);
			}
		]]>
	</fx:Script>
</s:Application>
